MACRO(GETTEXT_UPDATE_POT target _potFile)
	ADD_CUSTOM_TARGET(${target} COMMAND ${GETTEXT_XGETTEXT_EXECUTABLE} -o ${_potFile} ${ARGN})
ENDMACRO(GETTEXT_UPDATE_POT)

MACRO(GET_MO_DIR resultdir resultinstdir lang)
  IF (APPLE)
    SET(${resultdir} ${CMAKE_BINARY_DIR}/${PROJECT_NAME}.app/Contents/Resources/${lang}.lproj)
    SET(${resultinstdir} ${PROJECT_NAME}.app/Contents/Resources/${lang}.lproj)
  ELSE()
    SET(${resultdir} ${CMAKE_BINARY_DIR}/share/locale/${lang}/LC_MESSAGES)
    SET(${resultinstdir} share/locale/${lang}/LC_MESSAGES)
  ENDIF()
ENDMACRO(GET_MO_DIR)

MACRO(GETTEXT_INSTALL_PO basename)
	SET(_moFiles)
	SET(_addToAll)
	IF (GETTEXT_FOUND)
		SET(_addToAll "ALL")
	ENDIF()
	FOREACH (_currentPoFile ${ARGN})
		GET_FILENAME_COMPONENT(_lang ${_currentPoFile} NAME_WE)
		GET_MO_DIR(_moDir _moInstDir ${_lang})
		SET(_moFile ${_moDir}/${basename}.mo)
		SET(_moFiles ${_moFiles} ${_moFile})
		file(MAKE_DIRECTORY ${_moDir})

		ADD_CUSTOM_COMMAND( 
			OUTPUT ${_moFile} 
			COMMAND ${GETTEXT_MSGFMT_EXECUTABLE} -o ${_moFile} ${_currentPoFile}
			DEPENDS ${_currentPoFile} 
		)
		IF (GETTEXT_FOUND)
			INSTALL(FILES ${_moFile} DESTINATION ${_moInstDir})
		ENDIF()
	ENDFOREACH (_currentPoFile )

	ADD_CUSTOM_TARGET(translations ${_addToAll} DEPENDS ${_moFiles})
ENDMACRO(GETTEXT_INSTALL_PO)
