include(${CMAKE_SOURCE_DIR}/GetPrerequisites.cmake)
include(${CMAKE_SOURCE_DIR}/cmake/GetText.cmake)
include(${CMAKE_SOURCE_DIR}/cmake/InstallTranslations.cmake)
include(${CMAKE_SOURCE_DIR}/cmake/CopyWxTranslations.cmake)

macro(CopyDependencies target)
  set(searchdirs "")
  set(_libdirs)
  foreach(_file ${wxWidgets_LIBRARIES})
    string(REGEX REPLACE "^-L(.*)$" "\\1/dummy" _file ${_file})
    get_filename_component(dir ${_file} PATH)
    set(_libdirs ${_libdirs} ${dir})
  endforeach()

  foreach(_dir ${wxWidgets_LIBRARY_DIRS} ${_libdirs})
    if(IS_DIRECTORY ${_dir})
      list(APPEND searchdirs ${_dir})
    endif()
   endforeach()
  list(REMOVE_DUPLICATES searchdirs)

  if (APPLE)
    set(BUILD_DIR ${CMAKE_BINARY_DIR}/${PROJECT_NAME}.app/Contents/Frameworks)
    set(DESTINATION_DIR ../Frameworks)
  else ()
    set(BUILD_DIR ${CMAKE_BINARY_DIR}/bin)
    set(DESTINATION_DIR bin)
  endif ()

  get_target_property(targetfile ${target} LOCATION)

  set(prereqs)
  set(libraries_to_copy)
  get_prerequisites("${targetfile}" prereqs 1 1 "${searchdirs}" "")

  set(_dlls)
  foreach(ri ${prereqs})
    set(d d-NOTFOUND)
    find_file(d "${ri}" PATHS ${searchdirs} CMAKE_FIND_ROOT_PATH)
    find_file(d "${ri}" PATHS ${searchdirs} NO_CMAKE_FIND_ROOT_PATH)

    if(EXISTS ${d})
      GET_FILENAME_COMPONENT(_basename ${d} NAME)

      ADD_CUSTOM_COMMAND( 
        OUTPUT ${BUILD_DIR}/${_basename}
        COMMAND ${CMAKE_COMMAND} -E copy ${d} ${BUILD_DIR}/${_basename}
        DEPENDS ${d} 
      )
      if(NOT APPLE)
        install(FILES ${BUILD_DIR}/${_basename} DESTINATION ${DESTINATION_DIR})
      endif()
      list(APPEND _dlls ${BUILD_DIR}/${_basename})
    endif()
  endforeach()

 add_custom_target(copy_dlls_dependencies ALL DEPENDS ${_dlls})
endmacro(CopyDependencies)
